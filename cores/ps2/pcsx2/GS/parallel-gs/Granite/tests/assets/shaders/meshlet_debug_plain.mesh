#version 450
#extension GL_EXT_mesh_shader : require
#extension GL_EXT_shader_explicit_arithmetic_types_int16 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_ARB_shader_draw_parameters : require

#extension GL_KHR_shader_subgroup_basic : require
#if defined(MESHLET_PRIMITIVE_CULL_WAVE32) && MESHLET_PRIMITIVE_CULL_WAVE32
#extension GL_KHR_shader_subgroup_shuffle : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#endif

#ifndef MESHLET_SIZE
#error "Must define MESHLET_SIZE"
#endif

#if MESHLET_LOCAL_INVOCATION_INDEXED && !MESHLET_VERTEX_ID
shared uint shared_attr_index[MESHLET_SIZE];
shared vec4 shared_clip_pos[MESHLET_SIZE];
#define MESHLET_PRIMITIVE_CULL_SHARED_INDEX shared_indices
shared uint shared_indices[MESHLET_SIZE];
#endif

layout(max_primitives = MESHLET_SIZE, max_vertices = MESHLET_SIZE, triangles) out;
layout(local_size_x = 32, local_size_y_id = 0) in;

#include "meshlet_render_types.h"
#include "meshlet_primitive_cull.h"

#if MESHLET_VERTEX_ID
layout(location = 0) flat out uint vVertexID[];
layout(location = 1) perprimitiveEXT out uint vTransformIndex[];
layout(location = 2) perprimitiveEXT out uint vDrawID[];
#else
layout(location = 0) out mediump vec3 vNormal[];
layout(location = 1) out mediump vec4 vTangent[];
layout(location = 2) out vec2 vUV[];
layout(location = 3) perprimitiveEXT out uint vDrawID[];
#endif

layout(set = 1, binding = 0) uniform UBO
{
    mat4 VP;
};

layout(set = 1, binding = 2) uniform UBOViewport
{
    vec4 viewport;
};

layout(set = 0, binding = 0, scalar) readonly buffer IBO
{
    u8vec3 data[];
} ibo;

layout(set = 0, binding = 1, scalar) readonly buffer VBOPOS
{
    vec3 data[];
} pos;

struct TexturedAttr
{
    uint n;
    uint t;
    vec2 uv;
};

layout(set = 0, binding = 2, std430) readonly buffer VBOATTR
{
    TexturedAttr data[];
} attr;

layout(set = 0, binding = 3, std430) readonly buffer IndirectCommands
{
    IndirectDrawMesh draws[];
} indirect_commands_mesh;

#ifdef MESHLET_RENDER_TASK_HIERARCHICAL
taskPayloadSharedEXT CompactedDrawInfoPayload mesh_payload;
#else
layout(set = 0, binding = 4, std430) readonly buffer CompactedDraws
{
    CompactedDrawInfo infos[];
} mesh_payload;
#endif

layout(set = 0, binding = 5, std430) readonly buffer Transforms
{
    mat4 data[];
} transforms;

layout(set = 0, binding = 10) buffer Stats
{
    uint invocations;
    uint prim;
    uint vert;
} stats;

mediump vec4 unpack_bgr10a2(uint v)
{
    mediump ivec4 vs;
    vs.x = bitfieldExtract(int(v), 0, 10);
    vs.y = bitfieldExtract(int(v), 10, 10);
    vs.z = bitfieldExtract(int(v), 20, 10);
    vs.w = bitfieldExtract(int(v), 30, 3);
    return vec4(vs) / vec4(511.0, 511.0, 511.0, 1.0);
}

void main()
{
    uint compacted_meshlet_index = meshlet_get_meshlet_index();
#ifndef MESHLET_RENDER_TASK_HIERARCHICAL
    compacted_meshlet_index += 0x8000 * gl_DrawIDARB;
#endif

#if defined(MESHLET_RENDER_TASK_HIERARCHICAL) && !MESHLET_RENDER_TASK_HIERARCHICAL
    CompactedDrawInfo task = mesh_payload.info;
    task.meshlet_index += uint(mesh_payload.offsets[compacted_meshlet_index]);
#else
    CompactedDrawInfo task = mesh_payload.infos[compacted_meshlet_index];
#endif

    uint linear_index, sublet_index;
    if (gl_SubgroupSize == 32)
    {
        linear_index = gl_SubgroupInvocationID;
        sublet_index = gl_SubgroupID;
    }
    else
    {
        linear_index = gl_LocalInvocationID.x;
        sublet_index = gl_LocalInvocationID.y;
    }

    meshlet_setup_local_invocation(uvec2(linear_index, sublet_index));
    sublet_index = 8u * task.meshlet_index + meshlet_get_sublet_index(sublet_index);
    IndirectDrawMesh meshlet = indirect_commands_mesh.draws[sublet_index];

    mat4 M = transforms.data[task.node_offset];

    // Transform positions.
    vec3 pos = pos.data[meshlet.vertex_offset + linear_index];
    vec3 world_pos = (M * vec4(pos, 1.0)).xyz;
    vec4 clip_pos = VP * vec4(world_pos, 1.0);

    uvec3 prim = uvec3(0);
    if (linear_index < meshlet.primitive_count)
        prim = uvec3(ibo.data[meshlet.primitive_offset + linear_index]);
    meshlet_emit_primitive(prim, clip_pos, viewport);

    if (meshlet_lane_has_active_vert())
    {
        uint out_vert_index = meshlet_compacted_vertex_output();
        uint vert_id = meshlet.vertex_offset + linear_index;

#if MESHLET_VERTEX_ID
        gl_MeshVerticesEXT[out_vert_index].gl_Position = clip_pos;
        vVertexID[out_vert_index] = vert_id;
#elif defined(MESHLET_PRIMITIVE_CULL_SHARED_INDEX)
        shared_clip_pos[out_vert_index] = clip_pos;
        shared_attr_index[out_vert_index] = vert_id;
#else
        gl_MeshVerticesEXT[out_vert_index].gl_Position = clip_pos;
        TexturedAttr a = attr.data[vert_id];
        mediump vec3 n = unpack_bgr10a2(a.n).xyz;
        mediump vec4 t = unpack_bgr10a2(a.t);
        vUV[out_vert_index] = a.uv;
        vNormal[out_vert_index] = mat3(M) * n;
        vTangent[out_vert_index] = vec4(mat3(M) * t.xyz, t.w);
#endif
    }

#ifdef MESHLET_PRIMITIVE_CULL_SHARED_INDEX
    barrier();

    if (gl_LocalInvocationIndex < shared_active_vert_count_total)
    {
        TexturedAttr a = attr.data[shared_attr_index[gl_LocalInvocationIndex]];
        mediump vec3 n = unpack_bgr10a2(a.n).xyz;
        mediump vec4 t = unpack_bgr10a2(a.t);
        gl_MeshVerticesEXT[gl_LocalInvocationIndex].gl_Position = shared_clip_pos[gl_LocalInvocationIndex];
        vUV[gl_LocalInvocationIndex] = a.uv;
        vNormal[gl_LocalInvocationIndex] = mat3(M) * n;
        vTangent[gl_LocalInvocationIndex] = vec4(mat3(M) * t.xyz, t.w);
    }
#endif

    if (gl_LocalInvocationIndex < shared_active_prim_count_total)
    {
#ifdef MESHLET_PRIMITIVE_CULL_SHARED_INDEX
	    gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationIndex] = uvec3(unpack8(shared_indices[gl_LocalInvocationIndex]).xyz);
#endif
#if MESHLET_VERTEX_ID
        vTransformIndex[gl_LocalInvocationIndex] = task.node_offset;
#endif
        vDrawID[gl_LocalInvocationIndex] = task.meshlet_index;
    }

#if 0
    if (gl_LocalInvocationIndex == 0)
    {
        atomicAdd(stats.invocations, gl_WorkGroupSize.x * gl_WorkGroupSize.y);
        atomicAdd(stats.prim, shared_active_prim_count_total);
        atomicAdd(stats.vert, shared_active_vert_count_total);
    }
#endif
}

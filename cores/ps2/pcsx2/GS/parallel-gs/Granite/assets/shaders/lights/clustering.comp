#version 450
layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(rg32ui, set = 0, binding = 0) uniform writeonly uimage3D uCluster;
#ifdef INHERIT
layout(set = 0, binding = 1) uniform usampler3D uClusterInherit;
#endif

struct LightInfo
{
	mediump vec3 color;
	mediump float spot_outer;

	vec3 position;
	mediump float spot_inner;

	mediump vec3 direction;
	mediump float inv_radius;
};

layout(std140, set = 1, binding = 0) uniform SpotLight
{
    LightInfo lights[32];
} spot;

layout(std140, set = 1, binding = 1) uniform PointLight
{
    LightInfo lights[32];
} point;

layout(std140, set = 1, binding = 2) uniform SpotConstants
{
    vec3 cos_sin_size[32];
} spot_constants;

layout(std430, push_constant) uniform Registers
{
    mat4 inv_cluster_transform;
    ivec3 size;
    int size_z_log2;
    vec3 inv_texture_size;
    vec3 inv_size;
    float cube_radius;
    uint spot_count;
    uint point_count;
} registers;

void main()
{
    ivec3 id = ivec3(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(id.xy, registers.size.xy)))
        return;

    int slice = id.z >> registers.size_z_log2;
    float z_bias = slice == 0 ? 0.0 : 0.5;
    int wrapped_z = id.z & (registers.size.z - 1);

    uint point_mask = 0u;
    uint spot_mask = 0u;

    float world_scale_factor = exp2(max(0.0, float(slice - 1)));

#ifdef INHERIT
    uvec2 bits = textureLod(uClusterInherit, (vec3(id) + 0.5) * registers.inv_texture_size, 0.0).xy;
#else
    uint num_spot = registers.spot_count;
    uint num_point = registers.point_count;
#endif

    vec3 view_space =
        vec3(2.0, 2.0, 0.5) *
        (vec3(id.xy, wrapped_z) + 0.5) *
        registers.inv_size +
        vec3(-1.0, -1.0, z_bias);

    view_space *= world_scale_factor;
    vec3 cube_center = (registers.inv_cluster_transform * vec4(view_space, 1.0)).xyz;
    float cube_radius = registers.cube_radius * world_scale_factor;

#ifdef INHERIT
    while (bits.x != 0u)
#else
    for (uint i = 0u; i < num_spot; i++)
#endif
    {
#ifdef INHERIT
        uint i = uint(findLSB(bits.x));
#endif
        vec3 center = spot.lights[i].position;
        vec3 direction = spot.lights[i].direction;
        vec3 cos_sin_size = spot_constants.cos_sin_size[i];
        float size = cos_sin_size.z;

        // Sphere/cone culling from https://bartwronski.com/2017/04/13/cull-that-cone/.
        vec3 V = cube_center - center;
        float V_sq = dot(V, V);
        float V1_len  = dot(V, direction);
        float V2_len = sqrt(max(V_sq - V1_len * V1_len, 0.0));
        float distance_closest_point = cos_sin_size.x * V2_len - cos_sin_size.y * V1_len;

        if (!any(greaterThan(vec3(distance_closest_point, V1_len, -V1_len), vec3(cube_radius, cube_radius + size, cube_radius))))
            spot_mask |= 1u << i;
#ifdef INHERIT
        bits.x &= ~(1u << i);
#endif
    }

#ifdef INHERIT
    while (bits.y != 0u)
#else
    for (uint i = 0u; i < num_point; i++)
#endif
    {
#ifdef INHERIT
        uint i = uint(findLSB(bits.y));
#endif
        vec3 center = point.lights[i].position;
        float radius = 1.0 / point.lights[i].inv_radius;
        float radial_dist = distance(cube_center, center);
        bool radial_outside = radial_dist > (cube_radius + radius);

        if (!radial_outside)
            point_mask |= 1u << i;
#ifdef INHERIT
        bits.y &= ~(1u << i);
#endif
    }

    imageStore(uCluster, id, uvec4(spot_mask, point_mask, 0u, 0u));
}
